alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
WHITESPACE = _{ " " | "\t" | "\n" }

ID        = @{ alpha ~ (alpha | digit)* }
OPID      = @{ "op" ~ ("+" | "-" | "*" | "/")+ }
MACROID   = @{ alpha ~ (alpha | digit)* ~ "!" }
INT       = @{ digit+ }
FLOAT     = @{ digit+ ~ "." ~ digit+ }
INTIMAG   = @{ digit+ ~ "i" }
FLOATIMAG = @{ digit+ ~ "." ~ digit+ ~ "i" }
BINOP0    = @{ "||" }
BINOP1    = @{ "&&" }
BINOP2    = @{ "==" | "!=" }
BINOP3    = @{ "+" | "-" }
BINOP4    = @{ "*" | "/" }
UNOP5     = @{ "-" }
BINOP6    = @{ "." }
QUEST     = @{ "?" }

stmt = {
    SOI ~ stmt_assign
  | SOI ~ expr ~ ";"
}

stmt_assign = { ID ~ "=" ~ expr ~ ";" }

expr  = { expr1 ~ (BINOP0 ~ expr1)* }
expr1 = { expr2 ~ (BINOP1 ~ expr2)* }
expr2 = { expr3 ~ (BINOP2 ~ expr3)* }
expr3 = { expr4 ~ (BINOP3 ~ expr4)* }
expr4 = { expr5 ~ (BINOP4 ~ expr5)* }
expr5 = { UNOP5* ~ expr6 }
expr6 = { expr7 ~ (BINOP6 ~ expr7)* }
expr7 = { expr8 ~ ("(" ~ arg_list ~")")* }
expr8 = { expr_base }

expr_base = {
    matrix
  | list
  | lam_expr
  | let_expr
  | tuple
  | "(" ~ expr ~ ")"
  | bool
  | FLOATIMAG
  | INTIMAG
  | FLOAT
  | INT
  | OPID
  | MACROID
  | ID
}

matrix = { mat_row+ }
mat_row = { "{" ~ expr+ ~ "}" }

list = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]" }

lam_expr = { "lam" ~ param_list ~ ":" ~ expr }
param_list = { ID+ }

let_expr = { "let" ~ var_bindings ~ "in" ~ expr }
var_bindings = { ID ~ "=" ~ expr ~ ("," ~ ID ~ "=" ~ expr)* }

tuple = {
    "(" ~ ")"
  | "(" ~ expr ~ "," ~ ")"
  | "(" ~ expr ~ "," ~ expr ~ ")"
  | "(" ~ expr ~ ("," ~ expr)+ ~ ","? ~ ")"
}

bool = { "true" | "false" }

arg_list = { (arg ~ ("," ~ arg)*)? }
arg = {
    QUEST
  | expr
}
