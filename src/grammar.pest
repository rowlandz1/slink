alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
rangetoken = _{ ":" }
WHITESPACE = _{ " " | "\t" | "\n" }

ID        = @{ alpha ~ (alpha | digit)* }
OPID      = @{ "op" ~ ("+" | "-" | "*" | "/" | "%" | "==" | "!=" | "<=" | ">=" | "<" | ">" ) }
MACROID   = @{ alpha ~ (alpha | digit)* ~ "!" }
INT       = @{ digit+ }
FLOAT     = @{ digit+ ~ "." ~ digit+ }
INTIMAG   = @{ digit+ ~ "i" }
FLOATIMAG = @{ digit+ ~ "." ~ digit+ ~ "i" }
BINOP0    = @{ "||" }
BINOP1    = @{ "&&" }
BINOP2    = @{ "==" | "!=" | "<=" | ">=" | "<" | ">" }
BINOP3    = @{ "+" | "-" }
BINOP4    = @{ "*" | "/" | "%" }
BINOP5    = @{ "**" }
UNOP6     = @{ "-" }
BINOP7    = @{ "." | "$" }
QUEST     = @{ "_" }

prog = { stmt* }

stmt = {
    WHITESPACE* ~ stmt_assign
  | WHITESPACE* ~ expr ~ ";"
}

stmt_assign = { ID ~ "=" ~ expr ~ ";" }

expr  = { expr1 ~ (BINOP0 ~ expr1)* }
expr1 = { expr2 ~ (BINOP1 ~ expr2)* }
expr2 = { expr3 ~ (BINOP2 ~ expr3)* }
expr3 = { expr4 ~ (BINOP3 ~ expr4)* }
expr4 = { expr5 ~ (BINOP4 ~ expr5)* }
expr5 = { expr6 ~ (BINOP5 ~ expr6)* }
expr6 = { UNOP6* ~ expr7 }
expr7 = { expr8 ~ (BINOP7 ~ expr8)* }
//expr8 = { expr9 ~ ("(" ~ fun_args ~ ")")* }
expr8 = { expr9 ~ paren_operator* }
expr9 = { expr_base }

expr_base = {
    matrix
  | list
  | lam_expr
  | let_expr
  | tuple
  | "(" ~ expr ~ ")"
  | bool
  | string
  | FLOATIMAG
  | INTIMAG
  | FLOAT
  | INT
  | OPID
  | MACROID
  | ID
}

matrix = { mat_row+ }
mat_row = { "{" ~ expr ~ (","? ~ expr)* ~ "}" }

list = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]" }

lam_expr = { "lam" ~ param_list ~ ":" ~ expr }
param_list = { ID+ }

let_expr = { "let" ~ var_bindings ~ "in" ~ expr }
var_bindings = { ID ~ "=" ~ expr ~ ("," ~ ID ~ "=" ~ expr)* }

tuple = {
    "(" ~ ")"
  | "(" ~ expr ~ "," ~ ")"
  | "(" ~ expr ~ "," ~ expr ~ ")"
  | "(" ~ expr ~ ("," ~ expr)+ ~ ","? ~ ")"
}

bool = { "true" | "false" }

string = @{
    "'" ~ ("\\'" | (!"'" ~ ANY))* ~ "'"
  | "\"" ~ ("\\\"" | (!"\"" ~ ANY)* ~ "\"")
}

paren_operator = {
    "(" ~ kwarg_list ~ ")"
  | "(" ~ arg_list ~ ")"
  | "[" ~ slice ~ ("," ~ slice)? ~ "]"
}

arg_list = { (arg ~ ("," ~ arg)*)? }
arg = { QUEST | expr }
kwarg_list = { kwarg ~ ("," ~ kwarg)* }
kwarg = { ID ~ "=" ~ expr }

slice = { slice_ny | slice_nn | slice_yy | slice_yn | expr }
slice_ny = { rangetoken ~ expr }
slice_nn = { rangetoken }
slice_yy = { expr ~ rangetoken ~ expr }
slice_yn = { expr ~ rangetoken }
