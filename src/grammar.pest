alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
WHITESPACE = _{ " " | "\t" | "\n" }

ID        = @{ alpha ~ (alpha | digit)* }
OPID      = @{ "op" ~ ("+" | "-" | "*" | "/")+ }
INT       = @{ digit+ }
FLOAT     = @{ digit+ ~ "." ~ digit+ }
INTIMAG   = @{ digit+ ~ "i" }
FLOATIMAG = @{ digit+ ~ "." ~ digit+ ~ "i" }
BINOP0    = @{ "+" | "-" }
BINOP1    = @{ "*" | "/" }
UNOP2     = @{ "-" }
BINOP3    = @{ "." }
QUEST     = @{ "?" }

stmt = {
    SOI ~ stmt_assign
  | SOI ~ expr ~ ";"
}

stmt_assign = { ID ~ "=" ~ expr ~ ";" }

expr  = { expr1 ~ (BINOP0 ~ expr1)* }
expr1 = { expr2 ~ (BINOP1 ~ expr2)* }
expr2 = { UNOP2* ~ expr3 }
expr3 = { expr4 ~ (BINOP3 ~ expr4)* }
expr4 = { expr5 ~ ("(" ~ arg_list ~")")* }
expr5 = { expr_base }

expr_base = {
    matrix
  | list
  | tuple
  | lam_expr
  | let_expr
  | "(" ~ expr ~ ")"
  | FLOATIMAG
  | INTIMAG
  | FLOAT
  | INT
  | OPID
  | ID
}

matrix = { mat_row+ }
mat_row = { "{" ~ expr+ ~ "}" }

list = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]" }

tuple = {
    "(" ~ ")"
  | "(" ~ expr ~ "," ~ ")"
  | "(" ~ expr ~ "," ~ expr ~ ")"
  | "(" ~ expr ~ ("," ~ expr)+ ~ ","? ~ ")"
}

lam_expr = { "lam" ~ param_list ~ ":" ~ expr }
param_list = { ID+ }

arg_list = { (arg ~ ("," ~ arg)*)? }
arg = {
    QUEST
  | expr
}

let_expr = { "let" ~ var_bindings ~ "in" ~ expr }
var_bindings = { ID ~ "=" ~ expr ~ ("," ~ ID ~ "=" ~ expr)* }
